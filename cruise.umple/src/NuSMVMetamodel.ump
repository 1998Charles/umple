namespace cruise.umple.nusmv;

class NuSMVModule
{

  //depend cruise.umple.compiler.*;
  
  // Attributes declaration
  name; 
  startState;
  String [] args;
  
  //Associations of module object
  * -- 0..1 AssignPart;
  * -- 0..1 TransitionPart;
  * -- 0..1 DeclarationPart;
  
  // This defines the print method for nusmv modules
  output <<!<<#if(args.size() == 0){#>>

MODULE <<=getName()>><<#}
  else{
  	String temp = "";
  	if(args.size() > 1) {
  		for(int i = 0; i < args.size() - 1 ; i++)
  			temp += args.get(args.size()) + ",";
  		temp += args.get(args.size());
  	} 
  	else 
  		temp = args.get(0);
  		
  	#>>

MODULE <<=getName()>>( <<=temp>> )<<#}#>>

  <<#if(declarationPart != null){#>>

  <<=declarationPart.toString()>><<#}#>>
  <<#if(assignPart != null){#>>	
  <<=assignPart.toString(handle, dot)>><<#}#>>
  <<#if(getTransitionPart() != null){#>>
  <<=getTransitionPart().toString(handle, dot)>><<#}#>>

  !>>
  	emit toString(String handle,String dot)(output);
}

class AssignPart 
{
  name;
  startState;
  after constructor { setName("ASSIGN"); }
  
  output <<!<<=getName()>>
  	init(<<=handle>><<=dot>>state) := <<=startState>>;!>>
emit toString(String handle,String dot)(output);
}

class DeclarationStatement
{
  name;
  public String toString(){ return ""; }
}

class DeclarationPart
{
  name;
  * -- 1..* DeclarationStatement;
  after constructor { setName("VAR"); }
  
   
  private String writeStatements(int size)
  {
	int n = size;
	String temp = "";
  	if(n == 1) return declarationStatements.get(0).toString();
  	else {
  		int i = 0;
  		while(i < n - 1)
  		{
  			temp += declarationStatements.get(i).toString() + "\n    ";
  			i++;
  		}
  		temp += declarationStatements.get(i).toString();
  	}
  		return temp;
  }
  
  output <<!<<=getName()>> 
    <<=writeStatements(numberOfDeclarationStatements())>> !>>
  	emit toString()(output);
}

class EnumerationElement
{
  value;
}

class VarDeclarationStatement
{
  isA DeclarationStatement;
  type;
  String [] args;
  
  // This defines the print method for nusmv modules
  output <<!<<#if(args.size() == 0){#>><<=getName()>> : <<=getType()>>;<<#}
  else{
  	String temp = "";
  	if(args.size() > 1) {
  		for(int i = 0; i < args.size() - 1 ; i++)
  			temp += args.get(args.size()) + ",";
  		temp += args.get(args.size());
  	} 
  	else{ 
  		temp = args.get(0);#>><<=getName()>> : <<=getType()>>( <<=temp>> );<<#}
  	}#>>!>>
  	emit toString()(output);
}

class EnumerationStatement 
{
  isA DeclarationStatement;
  * -> 1..* EnumerationElement;
  
  private String writeElements(int size)
  {
	int n = size;
	String temp = "";
  	if(n == 1) return enumerationElements.get(0).getValue();
  	else {
  		int i = 0;
  		while(i < n - 1)
  		{
  			temp += enumerationElements.get(i).getValue() + " , ";
  			i++;
  		}
  		temp += enumerationElements.get(i).getValue();
  	}
  		return temp;
  }
  
  output <<!<<=  getName()>> : { <<=writeElements(numberOfEnumerationElements())>> }; !>>
  emit toString()(output);
  
}

class TransitionPart 
{
  name;
  after constructor { setName("TRANS"); } 
  * -- * ComplexTransition;
  * -- * SimpleTransition;
  * -- * NuSMVTransition;
  
  private String writeTransitions(int size, String handle, String dot)
  {
	int n = size;
	String temp = "";
  	if(n == 1) return nuSMVTransitions.get(0).toString(handle , dot);
  	else {
  		int i = 0;
  		while(i < n - 1)
  		{
  			temp += nuSMVTransitions.get(i).toString(handle , dot) + " &\n    ";
  			i++;
  		}
  		temp += nuSMVTransitions.get(i).toString(handle , dot);
  	}
  	return temp;
  }  
  
  output <<!
  <<=getName()>>
  <<#for(ComplexTransition ctrans : getComplexTransitions())
    addNuSMVTransition(ctrans);#>>
  <<#for(SimpleTransition strans : getSimpleTransitions())
   addNuSMVTransition(strans);#>>
<<=writeTransitions(numberOfNuSMVTransitions(), handle, dot)>>!>>
  emit toString(String handle, String dot)(output);
}

class NuSMVTransition
{
  fromState;
  nextState;
  event;
 
  public String toString(String handle,String dot){ return null; }
  //output <<!(transition = <<=getEvent()>> & next(state) = <<=getNextState()>>) !>>
  //emit print()(output);
}

class ComplexTransition 
{
  isA NuSMVTransition;
  * -- 2..* SimpleTransition;
  
  private String writeTransitions(int size,String handle,String dot)
  {
	int n = size;
	String temp = "";
  	if(n == 1) return simpleTransitions.get(0).printMe(handle , dot);
  	else {
  		int i = 0;
  		while(i < n - 1)
  		{
  			temp += simpleTransitions.get(i).printMe(handle , dot) + "|\n    ";
  			i++;
  		}
  		temp += simpleTransitions.get(i).printMe(handle , dot);
  	}
  		return temp;
  }
  
  output <<!( (<<=handle>><<=dot>>state = <<=simpleTransitions.get(0).getFromState()>>) -> (
    <<=writeTransitions(numberOfSimpleTransitions(), handle, dot)>>) )!>>
  emit toString(String handle , String dot)(output);
}

class SimpleTransition 
{
  isA NuSMVTransition;
  
  public String toString(String handle, String dot)
  {
    if(getEvent().contains("__autotransition") )
    	return printTransitionWithAutoEvent(handle,dot);
    if(getEvent() == "")
    	return printTransitionWithNoEvent(handle,dot);
    return printNormalTransition(handle,dot);
  }
  
  //This print transitions without an event, but source and target states.
  outputWithAutoEvent <<!( (<<=handle>><<=dot>>event = <<=getEvent()>>) -> (next(<<=handle>><<=dot>>state) = <<=getNextState()>>) )!>>
  	emit printTransitionWithAutoEvent(String handle,String dot)(outputWithAutoEvent);
  
  //This print transitions without an event, but source and target states.
  outputWithNoEvent <<!( (<<=handle>><<=dot>>state = <<=getFromState()>>) -> (next(<<=handle>><<=dot>>state) = <<=getNextState()>>) )!>>
  	emit printTransitionWithNoEvent(String handle,String dot)(outputWithNoEvent);
  
  //This prints transitions with event, and source and target states. 
  output <<!( (<<=handle>><<=dot>>state = <<=getFromState()>>) -> (
  	   <<=handle>><<=dot>>event = <<=getEvent()>> & next(<<=handle>><<=dot>>state) = <<=getNextState()>>) )!>>
  	emit printNormalTransition(String handle,String dot)(output);
  	
  //This prints transitions with event and traget state.	
  output1 <<!  (<<=handle>><<=dot>>event = <<=getEvent()>> & next(<<=handle>><<=dot>>state) = <<=getNextState()>>)  !>>
    emit printMe(String handle , String dot)(output1);
    
}