class Example {
	Integer warningRadius = 100;
	Boolean driveSelected;	
	Boolean lock;
  Boolean k = SmObjectDetectorObjectDetector.state == Emergency;
	sm { 
		CollisionAvoidance {
			ObjectDetector {
				Normal { objectDiscovered[warningRadius == 10] -> Warning; }	 
				Warning { [warningRadius <= 3] -> Emergency; }
				Emergency { -> Normal; }
			}
			||
			Brake {
				Released { 
					applyBrake -> AntiLockBraking; 
					[k] -> BrakeApplied;
				} 
				AntiLockBraking {
					[k] -> BrakeApplied;
					release -> Released;
					Inactive {
						wheelNormal -> BrakeApplied;
						wheelLocked[ lock == true ] -> ResolveLock;
					}
					ResolveLock {
						wheelNormal -> Inactive;
						wheelLocked -> MonitorDeclaration;
						decreasePressure[ lock == true ] -> PressureReduction;
						applyBrake[ lock == false ] -> BrakeApplied;
					}
					MonitorDeclaration {
						rapidDeclaration -> ResolveLock;
						wheelNormal -> MonitorDeclaration;
					}
					PressureReduction {
						decreasePressure[ lock == false ] -> ResolveLock;
						decreasePressure[ lock == true ] -> PressureReduction;
					}
					BrakeApplied {
						wheelLocked[ lock == true ] -> ResolveLock;
						applyBrake -> BrakeApplied;
					} 
				} 
			}
			||
			Transmission {
				ParkAndNeutral {
					[k] -> Park;
					selectReverse -> Reverse;
					selectDrive/{driveSelected = true; } -> Drive; 
					Park { selectneutral -> Neutral; }
					Neutral { selectPark/{driveSelected = false;} -> Park; } 
				}
				Reverse { 
					selectPark -> Park; 
					[k] -> Park;
				} 
				Drive { 
					selectNeutral -> Neutral; 
					selectFirst -> First;
					selectSecond -> Second;
					[k] -> Park;
					First { 
						reachSecondSpeed[ driveSelected ] -> Second; 
					}
					Second { 
						reachThirdSpeed[ driveSelected ] -> Third;
					}
					Third { 
						dropBelowThirdSpeed -> Second; 
					}
				} 
			}
			||
			Engine {
				Off { 
					turnOn -> Idle;
				}
				Idle { 
					accelerate -> Driving; 
					[k] -> Off;
				}
				Driving { 
					release -> Idle;
					applyBrake -> Idle; 
					[k] -> Off;
				} 
			} 
		} 
	}	 
}
